class Main {
	static int selectedLevel;

	function void main(){
		do UI.init();
		do Main.goToMainMenu();
		return;
	}
	// гит - изобретение сатаны

	function void goToMainMenu(){
		var int key;
		do Screen.clearScreen();
		do UI.drawMainMenu();
		do Sys.wait(250);
		while (true){
			let key = Keyboard.keyPressed();
			if (key = Keys.enter()){
				do Main.goToLevelMenu();
			}
			if (key = Keys.esc()){
				do Sys.halt();
			}
		}
		return;
	}

	function void goToLevelMenu(){
		var int key;
		var Game game;
		let selectedLevel = 1;
		do Screen.clearScreen();
		do UI.drawLevelMenu();
		do UI.highlightSelectedLevel(selectedLevel);
		do Sys.wait(250);
		while (true){
			let key = Keyboard.keyPressed();

			if (key = Keys.right()){
				if (selectedLevel < 6){
					let selectedLevel = selectedLevel + 1;
					do UI.highlightSelectedLevel(selectedLevel);
				}
				do Sys.wait(250);
			}
			if (key = Keys.left()){
				if (selectedLevel > 1){
					let selectedLevel = selectedLevel - 1;
					do UI.highlightSelectedLevel(selectedLevel);
				}
				do Sys.wait(250);
			}

			//запуск уровня
			if (key = Keys.enter()){
				let game = Game.new(selectedLevel);
				do Screen.clearScreen();
				do game.start();
				//конец уровня
				do Screen.clearScreen();
				do UI.drawLevelMenu();
				do UI.highlightSelectedLevel(selectedLevel);
				do Sys.wait(250);
			}

			if (key = Keys.esc()){
				do Main.goToMainMenu();
			}
		}
		return;
	}
}